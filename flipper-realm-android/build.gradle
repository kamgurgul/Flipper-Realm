/*
 * Copyright 2019 KG Soft
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def packageName = 'com.kgurgul.flipper'
def pluginVersion = '1.0.0'

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName pluginVersion
    }
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = packageName
                artifactId = project.getName()
                version = pluginVersion
                artifact(bundleReleaseAar)
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if (it.group != null
                                && (it.name != null || "unspecified" == it.name)
                                && it.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.username")
    key = properties.getProperty("bintray.apikey")
    publications = ['maven']
    pkg {
        repo = 'flipper'
        name = project.getName()
        licenses = ['Apache-2.0']
        desc = 'Android Realm database driver for Facebook Flipper'
        websiteUrl = 'https://github.com/kamgurgul/Flipper-Realm'
        issueTrackerUrl = 'https://github.com/kamgurgul/Flipper-Realm/issues'
        vcsUrl = 'https://github.com/kamgurgul/Flipper-Realm.git'
        version {
            name = pluginVersion
            released  = new Date()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    compileOnly "io.realm:realm-android-library:$realm_version"
    compileOnly "com.facebook.flipper:flipper:$flipper_version"
}